Note: rename this file to index.adoc to make it active.
The default rule conflicts with default.xml, so only one of both may be active.

= Coding Rules

The following constraints apply for modules:

[[default]]
[role=group,includesConstraints="debug:rule(info),metrics:*",includesConcepts="structure:*"]
- <<debug:rule>>
- <<package:PackageLevel>>
- <<dependency:TransitivePackageDependencies>>
- <<dependency:WrongDirection>>
- <<structure:MarkAsciidocTypeRegex>>
- <<structure:LabelTypesMatchedByRegex>>
- <<metrics:MaxSpotBugsWarnings>>
- <<metrics:MaxCheckstyleErrors>>
- <<metrics:MaxCheckstyleWarnings>>
- <<metrics:MaxPmdWarnings>>

[[debug:rule]]
.For testing and debugging purposes.
[source,cypher,role=constraint]
----
MATCH
    (p:Asciidoc:IgnoreMe)
RETURN
    count(p);
----

This kind of Technical Debt is about module dependencies which are in the code,
but are not desired.

[[documented:TechnicalDebt]]
.Creates a relationship between two Packages for Technical Debt.
[source,cypher,role=concept]
----
match (a:Asciidoc:Table)-[:BODY]->(body)
where a.title='Unwantend Module Dependencies'
with body
match (c1:Asciidoc:Cell {colnumber: 0})<-[:CONTAINS_CELLS]-(body)-[:CONTAINS_CELLS]->(c2:Asciidoc:Cell {colnumber: 1})
with c1, c2
			MATCH
				(m1:Java:Package), (m2:Java:Package)
			WHERE
				m1.fqn CONTAINS trim(c1.text)
			AND
				m2.fqn CONTAINS trim(c2.text)
			MERGE
				(m1)-[:TECHNICAL_DEBT]->(m2)
			RETURN
				m1, m2;

----

There is also some Technical Debt regarding SpotBugs.
This query works with the old FindBugs file format which the Gradle plugin produces.

[[metrics:MaxSpotBugsWarnings]]
.There must be less than X warnings reported by SpotBugs
[source,cypher,role=constraint]
----
    MATCH
        (c:FindBugs:BugInstance)-[:HAS_SOURCELINE]->(s:FindBugs:SourceLine)
    WHERE
        not(c.category='PERFORMANCE')
    RETURN
        s.fqn, s.start, c.priority, c.category, c.type
    ORDER BY
        c.priority, c.category, c.type
    SKIP
        31
----

[[metrics:MaxCheckstyleErrors]]
.There must be less than X errors reported by Checkstyle.
[source,cypher,role=constraint]
----
    MATCH
        (n:Checkstyle:Error {severity: 'error'})--(f:Checkstyle:File)
    RETURN
        n.message,f.name, n.line
    SKIP
        21
----

[[metrics:MaxCheckstyleWarnings]]
.There must be less than X warnings reported by Checkstyle.
[source,cypher,role=constraint]
----
    MATCH
        (n:Checkstyle:Error {severity: 'warning'})--(f:Checkstyle:File)
    RETURN
        n.message,f.name, n.line
    SKIP
        1
----

[[metrics:MarkAsciidocTypeMetric]]
.Enrich Asciidoc cells for PMD constraints.
[source,cypher,role=concept]
----
    MATCH
        (a:Asciidoc:Table)-[:HAS_ROW]->(body),
        (a)-[:HAS_ATTRIBUTE]->(att:Asciidoc:Attribute)
    WHERE
        att.name='metric' AND att.value='pmd'
    WITH
        body
    MATCH
        (body)-[:CONTAINS_CELL]->(prioCell:Asciidoc:Cell {colnumber: 0}),
        (body)-[:CONTAINS_CELL]->(thresholdCell:Asciidoc:Cell {colnumber: 1})
    SET
        prioCell:Pmd:PriorityCell,
        thresholdCell:Pmd:ThresholdCell
    CREATE UNIQUE
        (prioCell)-[:HAS_UPPER_THRESHOLD]->(thresholdCell)
    RETURN
        prioCell, thresholdCell
----

[[metrics:MaxPmdWarnings]]
.There must be less than X warnings reported by PMD.
[source,cypher,role=constraint,requiresConcepts="metrics:MarkAsciidocTypeMetric"]
----
    MATCH
        (prioCell:Pmd:PriorityCell)-[:HAS_UPPER_THRESHOLD]->(thresholdCell)
    WITH
        prioCell, thresholdCell
    MATCH
        (f:Pmd:File)-[:HAS_VIOLATION]->(v:Pmd:Violation)
    WHERE
        v.priority=toInt(prioCell.text)
    WITH
        collect(v) as vios, thresholdCell
    WITH
    CASE
        WHEN length(vios) > toInt(thresholdCell.text)
        THEN vios
    END AS out
    UNWIND
        out AS v
    RETURN
        v.package+"."+v.className+":"+v.beginLine as location, v.priority, v.message
    ORDER BY
        v.priority
----


== Find package dependencies in the wrong direction

The desired package dependencies are described in an Asciidoc document
which also contains PlantUML package diagrams. These diagrams were
imported into the jQAssistant database. The following Concepts and
Constraints were neede to find dependencies in the wrong direction.

[[package:PackageLevel]]
.Set the level property of a package, e.g. 1 for de, 2 for de.kontext_e and so on.
[source,cypher,role=concept,requiresConcepts="dependency:Package"]
----
MATCH
    (p:Java:Package)
WITH
    SPLIT(p.fqn,".") AS splitted, p
SET
    p.level=SIZE(splitted)
RETURN
    splitted, SIZE(splitted);
----

[[dependency:TransitivePackageDependencies]]
.Add a DEPENDS_ON relationship to parents of a package P from other packages up to the same level of the source package.
[source,cypher,role=concept,requiresConcepts="package:PackageLevel"]
----
MATCH
    (p:Java:Package)-[:DEPENDS_ON]->(p2:Java:Package),
    (parent:Java:Package)-[:CONTAINS*]->(p2:Java:Package)
WHERE
    p.level <= parent.level
CREATE UNIQUE
    (p)-[:DEPENDS_ON]->(parent)
RETURN
    p.fqn, parent.fqn;
----

[[dependency:WrongDirection]]
.Finds package dependencies which are in the wrong direction according to the documentation.
[source,cypher,role=constraint,requiresConcepts="dependency:TransitivePackageDependencies",severity=critical]
----
MATCH
    (p1:PlantUml:Package)-[:MAY_DEPEND_ON]->(p2:PlantUml:Package),
    (p3:Java:Package)-[:DEPENDS_ON]->(p4:Java:Package)
WHERE
    p1.fqn = p4.fqn
    AND p2.fqn = p3.fqn
RETURN
    p3.fqn + "-->" + p4.fqn AS WrongDirection;
----

== Enhance Graph with design information from Architecture Documentation

[[structure:MarkAsciidocTypeRegex]]
[source,cypher,role=concept]
.Mark Asciidoc Table Cells that contain regular expressions for types that have to be marked with given labels.
----
    MATCH
        (a:Asciidoc:Table)-[:HAS_ROW]->(body),
        (a)-[:HAS_ATTRIBUTE]->(att:Asciidoc:Attribute)
    WHERE
        att.name='label' AND att.value='Pattern'
    WITH
        body
    MATCH
        (body)-[:CONTAINS_CELL]->(regexCell:Asciidoc:Cell {colnumber: 0}),
        (body)-[:CONTAINS_CELL]->(labelCell:Asciidoc:Cell {colnumber: 1})
    SET
        regexCell:RegularExpressionCell,
        labelCell:LabelCell
    CREATE UNIQUE
        (regexCell)-[:REGEX_FOR_LABEL]->(labelCell)
    RETURN
        regexCell, labelCell
----


[[structure:LabelTypesMatchedByRegex]]
[source,js,role=concept,requiresConcepts="structure:MarkAsciidocTypeRegex"]
.Mark types with labels matched by regex.
----
    // Define the columns returned by the constraint
    var columnNames = java.util.Arrays.asList("Type");
    // Define the list of rows returned by the constraint
    var rows = new java.util.ArrayList();

    var result = store.executeQuery("    MATCH\n" +
                                                       "        (type:Type),\n" +
                                                       "        (regexCell:RegularExpressionCell)-[:REGEX_FOR_LABEL]->(labelCell:LabelCell)\n" +
                                                       "    WHERE\n" +
                                                       "        type.fqn =~ regexCell.text\n" +
                                                       "    RETURN\n" +
                                                       "        type, labelCell.text as label\n").iterator();

    while(result.hasNext()) {
        var next = result.next();
        var node = next.get("type", Java.type("com.buschmais.xo.api.CompositeObject").class).getDelegate();
        var label = next.get("label", java.lang.String.class);
        node.addLabel(new com.buschmais.xo.neo4j.embedded.impl.model.EmbeddedLabel(label));
        var resultRow = new java.util.HashMap();
        resultRow.put("Class", node);
        rows.add(resultRow);
    }

    // Return the result
    var status = com.buschmais.jqassistant.core.analysis.api.Result.Status.SUCCESS;
    new com.buschmais.jqassistant.core.analysis.api.Result(rule, status, severity, columnNames, rows);
----

